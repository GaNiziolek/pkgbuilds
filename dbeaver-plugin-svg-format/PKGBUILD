# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-svg-format
pkgver=1.0.121.202207091909
pkgrel=1
pkgdesc='DBeaver plugin to save diagrams in SVG format'
arch=('any')
url='https://dbeaver.io/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-batik')
source=("https://dbeaver.io/update/ce/latest/plugins/org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
        "https://dbeaver.io/update/ce/latest/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('1fb33c9c5a37030bde9478fd61768259826b117b9a583341a78a20424b53eb1f'
            '2966764bfe84203bbcef343e4ed09de6021e2cfd15035939426cea269736c17e'
            '793138d8ff832da211c2874551daf6b21b06fdf6c97c91858378587d79e76660')
noextract=("org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
           "ui.svg.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
